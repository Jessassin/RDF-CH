#COPYRIGHT (C) Jessassin 2013
#This file is part of the Redstone Development Foundation's CommandHelper Script Library (RDF-CH)
# LICENSED GNU Lesser General Public License (GNU LGPL) version  3
# See LICENSE.txt or visit http://www.gnu.org/licenses/ for more information


#=========================================================================#
#
# RDF Server Worldedit Limiter
# Authored by: Jessassin
# 
# Requires: Worldedit, RDF Commons 1.1+, CH.all
# Risk: low/none
#
#=========================================================================#

#all:/distance [$=''] = >>>
#commons version check!
#assign(@requiredcommonsversion,1.1)
#_rdf_checkcommonsversion(@requiredcommonsversion)
##
#assign(@command,'/distance')
#assign(@arguments,'<type>')
#assign(@var, $)
#include('/includes/distance.ms')
#if(equals_ic(@var, 3d), die(_rdf_3d_distance(sk_pos1(), sk_pos2())))
#if(equals_ic(@var, horizontal), die(_rdf_2d_distance(sk_pos1(), sk_pos2())))
#if(equals_ic(@var, max_horizontal), die(_rdf_max_horizontal_distance(sk_pos1(), sk_pos2())))
#if(equals_ic(@var, vertical), die(_rdf_vertical_distance(sk_pos1(), sk_pos2())))
#if(equals_ic(@var, volume), die(_rdf_cuboid_volume(sk_pos1(), sk_pos2())))
#msg('Syntax error! /distance <type>')
#die('Acceptable types: 3d, horizontal, max_horizontal, vertical')
#<<<

#Worldedit restrictions for max size (matches max plot size of 256x256, and prevents accidental worldedit greifs which have happened in the past)
*://set [$='null'] = >>>
if(not(has_permission('worldedit.region.set')),
	die('Permission error!')
)
#commons version check!
assign(@requiredcommonsversion,1.1)
_rdf_checkcommonsversion(@requiredcommonsversion)
##
	include('/includes/config.txt')
	assign(@option,$)
	assign(@command,'//set')
	assign(@arguments,'<block>')
	if(lt(length(@option),1),die(_rdf_syntaxerror(@command,@arguments)),
		if(gt(_rdf_max_horizontal_distance(sk_pos1(), sk_pos2()), @maxdistance),
			die(concat('This command is limited to a max horizontal distance of ', @maxdistance)),
			assign(@command, concat('//set ', @option))
			run(@command)
	)
	)
<<<

*://replace [$1='null'] [$2='null'] = >>>
if(not(has_permission('worldedit.region.replace')),
	die('Permission error!')
)
#commons version check!
assign(@requiredcommonsversion,1.1)
_rdf_checkcommonsversion(@requiredcommonsversion)
##
	include('/includes/config.txt')
	assign(@option1, $1)
	assign(@option2, $2)
	if(gt(_rdf_max_horizontal_distance(sk_pos1(), sk_pos2()), @maxdistance),
		die(concat('This command is limited to a max horizontal distance of ', @maxdistance)),
		assign(@command, concat('//replace ',@option1, ' ', @option2))
		run(@command)
	)
<<<

*://copy = >>>
if(not(has_permission('worldedit.clipboard.copy')),
	die('Permission error!')
)
#commons version check!
assign(@requiredcommonsversion,1.1)
_rdf_checkcommonsversion(@requiredcommonsversion)
##
	include('/includes/config.txt')
	if(gt(_rdf_max_horizontal_distance(sk_pos1(), sk_pos2()), @maxdistance),
		die(concat('This command is limited to a max horizontal distance of ', @maxdistance)),
		assign(@command, '//copy')
		run(@command)
	)
<<<

*://stack [$1] [$2] [$3] = >>>
assign(@args, array($1, $2, $3))
if(not(has_permission('worldedit.region.stack')),
	die('Permission error!')
)
# commons version check!
assign(@requiredcommonsversion,1.1)
_rdf_checkcommonsversion(@requiredcommonsversion)
	if(equals(@args, array('','','')),
		run('//stack')
		die()
	)
	assign(@dirs, array('south','west','north','east','down','up'))
	if(is_numeric(@args[0]),
		assign(@stacks, integer(@args[0]))
		assign(@dir, @args[1])
		assign(@cla, 0)
		, #else
		assign(@stacks, integer(@args[1]))
		assign(@dir, @args[2])
		assign(@cla, 1)
	)
	if(array_contains(array('n', 'e', 's', 'w', 'u', 'd'), @dir),
		assign(@dir, array('n':'north', 'e':'east', 's':'south', 'w':'west', 'u':'up', 'd':'down')[@dir])
	)
	if(not(array_contains(@dirs, @dir)),
		assign(@pfac, round(pfacing()[0]))
		assign(@transd, mod(add(@pfac, 45), 360))
		assign(@direction, floor(divide(@transd, 90)))
		
		assign(@vert, pfacing()[1])
		if(gt(@vert, 68),
			assign(@direction, 4)
		)
		if(lt(@vert, -68),
			assign(@direction, 5)
		)
		, #else
		assign(@dir, to_lower(@dir))
		assign(@direction, array('south':0,'west':1,'north':2,'east':3,'down':4,'up':5)[@dir])
	)
	if(or(equals(@direction, 4), equals(@direction, 5)),
		assign(@horizd, abs(subtract(sk_pos2()[1], sk_pos1()[1])))
		, #else
		if(or(equals(@direction, 0), equals(@direction, 2)),
			assign(@horizd, abs(subtract(sk_pos2()[2], sk_pos1()[2])))
			, #else
			assign(@horizd, abs(subtract(sk_pos2()[0], sk_pos1()[0])))
		)
	)
	assign(@horizd, add(@horizd, 1))
	
	assign(@totalhorizd, multiply(@horizd, @stacks))
	include('/includes/config.txt')
	if(gt(@totalhorizd, @maxdistance),
		die(concat('This command is limited to a max horizontal distance of ', @maxdistance))
	)
	if(equals(@cla, 1),
		array_set(@args, 2, array('south','west','north','east','down','up')[@direction])
		, #else
		array_set(@args, 1, array('south','west','north','east','down','up')[@direction])
	)
	assign(@command, sconcat('//stack', array_implode(@args, ' ')))
	run(@command)
<<<
